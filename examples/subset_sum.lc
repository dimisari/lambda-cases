
sub_set_sum(_, _) : ListOf(Int)s x Int => Possibly(ListOf(Int)s)
  = (?, ?)
    (*, 0) => a_value--[]
    ([], *) => no_value
    ([x, xs = ...], i) =>
      (x > i) -> ?
        true => sub_set_sum(xs, i)
        false =>
          sub_set_sum(xs, i - x) -> ?
            a_value => a_value--(x + the_value)
            no_value => sub_set_sum(xs, i)

list : ListOf(Int)s
  = [1, 9, 4, 5, 6]

res_msg : Possibly(ListOf(Int)s) => String
  = ?
    no_value => "Not possible"
    a_value => "Here is the subset: " + the_value

try_the_number(_) : Int => Program
  = i =>
    print("Trying " + (i)to_string) ;
    sub_set_sum(list, i) -> res_msg -> print(_)

try_the_list(_) : ListOf(Int)s => Program
  = ?
    [] => do_nothing
    [i, is = ...] => try_the_number(i) ; try_the_list(is)

from(_)to(_) : Int^2 => ListOf(Int)s
  = (i, j) =>
    (i > j) -> ?
      true => []
      false => i + from(i + 1)to(j)

main : Program
  = try_the_list(from(15)to(25))
