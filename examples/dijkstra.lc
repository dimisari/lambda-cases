type nickname: Graph = ListOf(Node)s

tuple type: Node = Char x ListOf(Neighbor)s
field names: (n_name, neighbors)

type nickname: Dist = Int

tuple type: NameAndDist = Char x Dist
field names: (name, dist)

type nickname: Neighbor = NameAndDist

tuple type: State = ListOf(DoneNode)s x ListOf(ReachableNode)s
field names: (dns, rns)

or type: InRNsPossibilities
values:
  in_rns--<dist_and_rns : Dist x ListOf(ReachableNode)s> | not_in_rns

type nickname: DoneNode = NameAndDist

type nickname: ReachableNode = NameAndDist

type nickname: StateManipulationWith(T1) = (T1)FState(State)Man

type nickname: StateMan = State(State)Man

set_rns(_) : ListOf(ReachableNode)s => StateMan
  = rns => modify_state_with(s => (s.dns, rns))

dijkstra(_, _) : Char x Graph => ListOf(DoneNode)s
  = (source_name, g) => result_of(get_result(g))on_init_state(is)
    where
    is : State
      = ([], [(source_name, 0)])

get_result(_) : Graph => StateManipulationWith(ListOf(DoneNode)s)
  = g =>
    get_state >> state =>
    state.rns -> ?
      [] => wrap(state.dns)
      [rn1, rns = ...] =>
        set_state((state.dns + rn1, rns)) ;
        add_new_rns(rn1, get_neighbors(rn1, g)) ;
        get_result(g)

get_neighbors(_, _) : DoneNode x Graph => ListOf(Neighbor)s
  = (dn, g) => get_neighbors_from_nodes(g)
    where
    get_neighbors_from_nodes(_) : ListOf(Node)s => ListOf(Neighbor)s
      = ?
        [] => throw_err("Should be impossible: I'm not in the nodes!")
        [n, ns = ...] =>
          (dn.name == n.n_name) -> ?
            true => n.neighbors
            false => get_neighbors_from_nodes(ns)

update_neighbor(_)of(_) : Neighbor x DoneNode => StateMan
  = (ne, dn) =>
    check_if_ne_in_rns >> ?
      in_rns => add_ne_to_rns_from_dn_if_better(dist_and_rns)
      not_in_rns =>
        check_if_ne_in_dns >> ?
          true => do_nothing
          false => add_ne_to_rns_from_dn
    where
    check_if_ne_in_rns : StateManipulationWith(InRNsPossibilities)
      = get_state >> _.rns o> check_ne_in_rns_rec([], _)
        where
        check_ne_in_rns_rec(_, _)
          : ListOf(ReachableNode)s^2 => StateManipulationWith(InRNsPossibilities)
          = (checked, ?)
            [] => wrap(not_in_rns)
            [rn1, rns = ...] =>
              (ne.name == rn1.name) -> ?
                true => in_rns--(rn1.dist, checked + rns) -> wrap(_)
                false => check_ne_in_rns_rec(checked + rn1, rns)

    add_ne_to_rns_from_dn_if_better(_)
      : Dist x ListOf(ReachableNode)s => StateMan
      = (d, other_rns) =>
        (new_d < d) -> ?
          true => set_rns(_) <- add((ne.name, new_d))to_rns(other_rns)
          false => do_nothing
        where
        new_d : Dist
          = dn.dist + ne.dist

    check_if_ne_in_dns : StateManipulationWith(Bool)
      = get_state >> _.dns o> check_ne_name_in(_)
        where
        check_ne_name_in(_)
          : ListOf(DoneNode)s => StateManipulationWith(Bool)
          = ?
            [] => wrap(false)
            [dn, dns = ... ] =>
              (ne.name == dn.name) -> ?
                true => wrap(true)
                false => check_ne_name_in(dns)

    add_ne_to_rns_from_dn : StateMan
      = get_state >> _.rns o> add((ne.name, dn.dist + ne.dist))to_rns(_) o>
        set_rns(_)

add_new_rns(_, _) : DoneNode x ListOf(Neighbor)s => StateMan
  = (dn, ?)
    [] => do_nothing
    [ne1, nes = ...] => update_neighbor(ne1)of(dn) ; add_new_rns(dn, nes)

add(_)to_rns(_)
  : ReachableNode x ListOf(ReachableNode)s => ListOf(ReachableNode)s
  = (rn, ?)
    [] => [rn]
    [rn1, rns = ...] =>
      (rn.dist < rn1.dist) -> ?
        true => rn + (rn1 + rns)
        false => rn1 + add(rn)to_rns(rns)

test_graph : Graph
  = [ ('a', [('b', 2), ('d', 6)])
    , ('b', [('a', 2), ('c', 3)])
    , ('c', [('b', 3), ('d', 5), ('e', 1)])
    , ('d', [('a', 6), ('c', 5), ('e', 4)])
    , ('e', [('c', 1), ('d', 4), ('f', 3)])
    , ('f', [('c', 7), ('e', 3)])
    ]

type_theorem (NameAndDist)Has_Str_Rep
proof
  (_)to_string = nam => "(name = " + nam.name + ", dist = " + nam.dist + ")"

main : Program
  = dijkstra('a', test_graph) -> print

