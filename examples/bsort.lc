
type nickname: IsSorted = Bool

bubble_sort_pass(_): ListOf(Int)s => ListOf(Int)s x IsSorted
  = ?
    [] => ([], true)
    [x] => ([x], true)
    [x1, x2, rest = ...] =>
      (x1 > x2) -> ?
        true => prepend(x2)to(bubble_sort_pass(x1 + rest)) -> not_sorted(_)
        false => prepend(x1)to(bubble_sort_pass(x2 + rest))
      where
      not_sorted(_)
        : ListOf(Int)s x IsSorted => ListOf(Int)s x IsSorted
        = _.change{2nd = false}

      prepend(_)to(_)
        : Int x (ListOf(Int)s x IsSorted) => ListOf(Int)s x IsSorted
        = (elem, (l, b)) => (elem + l, b)

bubble_sort(_): ListOf(Int)s => ListOf(Int)s
  = bubble_sort_pass(_) o>
    (l, ?)
      true => l
      false => bubble_sort(l)

initial: ListOf(Int)s
  = compute_list(16, 65)
    where
    compute_list(_, _): Int^2 => ListOf(Int)s
      = (?, seed)
        0 => []
        n =>
          new_seed + compute_list(n - 1, new_seed)
          where
          new_seed: Int
            = (seed * 137 + 236 - n)mod(101)

main: Program
  = print("Initial list = " + initial);
    print("Sorted list = " + bubble_sort(initial))
